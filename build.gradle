plugins {
	id 'org.springframework.boot' version '2.3.4.RELEASE'
	id 'io.spring.dependency-management' version '1.0.10.RELEASE'
	id 'java'
	id 'java-library'
	id 'maven-publish'
	id 'net.researchgate.release' version '2.8.1'
	id 'com.github.ben-manes.versions' version '0.33.0'
	id 'com.github.breadmoirai.github-release' version '2.2.12'
	id 'com.jfrog.bintray' version '1.8.5'
}

group = 'com.redislabs'
description = 'Spring Batch Redis'
sourceCompatibility = '1.8'

repositories {
	jcenter()
	mavenCentral()
	mavenLocal()
}

bootJar {
  enabled = false
}

jar {
  enabled = true
}

dependencies {
	compileOnly 'org.projectlombok:lombok'
	annotationProcessor 'org.projectlombok:lombok'
	api 'io.lettuce:lettuce-core:5.3.4.RELEASE'
	api 'org.apache.commons:commons-pool2'
	implementation 'org.springframework.boot:spring-boot-starter-batch'
	testImplementation('org.springframework.boot:spring-boot-starter-test') {
		exclude group: 'org.junit.vintage', module: 'junit-vintage-engine'
	}
	testImplementation 'org.springframework.batch:spring-batch-test'
	testImplementation 'org.hsqldb:hsqldb'
	testImplementation 'org.junit.jupiter:junit-jupiter-api'
	testImplementation 'org.junit.jupiter:junit-jupiter-engine'
	testImplementation 'org.junit.jupiter:junit-jupiter-params'
	testImplementation 'org.testcontainers:testcontainers:1.15.0-rc2'
	testImplementation 'org.testcontainers:junit-jupiter:1.15.0-rc2'
	testImplementation 'com.fasterxml.jackson.dataformat:jackson-dataformat-csv:2.11.2'
	testImplementation 'org.projectlombok:lombok'
	testAnnotationProcessor 'org.projectlombok:lombok'
}

test {
	useJUnitPlatform()
}

bintray {
	user = project.hasProperty('bintrayUser') ? project.property('bintrayUser') : System.getenv('BINTRAY_USER')
	key = project.hasProperty('bintrayKey') ? project.property('bintrayKey') : System.getenv('BINTRAY_KEY')
	publications = ['mavenJava']
	publish = true
	pkg {
		repo = 'maven'
		name = 'spring-batch-redis'
		licenses = ['Apache-2.0']
		vcsUrl = 'https://github.com/Redislabs-Solution-Architects/spring-batch-redis.git'
		version {
			gpg {
				sign = true
			}
			mavenCentralSync {
				sync = true
				user = project.hasProperty('ossrhUsername') ? project.property('ossrhUsername') : System.getenv('OSSRH_USERNAME')
				password = project.hasProperty('ossrhPassword') ? project.property('ossrhPassword') : System.getenv('OSSRH_PASSWORD')
			}
		}
	}
}

task sourcesJar(type: Jar) {
	archiveClassifier = 'sources'
	from sourceSets.main.allJava
}

task javadocJar(type: Jar) {
	archiveClassifier = 'javadoc'
	from javadoc.destinationDir
}

artifacts {
	archives sourcesJar, javadocJar
}

publishing {
	publications {
		mavenJava(MavenPublication) {
			from components.java
			artifact sourcesJar
			artifact javadocJar
			groupId 'com.redislabs'
			artifactId 'spring-batch-redis'

			pom {
				name = 'Spring Batch Redis'
				packaging = 'jar'
				description = 'Spring Batch extension for Redis'
				url = 'https://github.com/Redislabs-Solution-Architects/spring-batch-redis'

				scm {
					connection = 'scm:git:git://github.com/Redislabs-Solution-Architects/spring-batch-redis.git'
					developerConnection = 'scm:git:git@github.com:Redislabs-Solution-Architects/spring-batch-redis.git'
					url = 'https://github.com/Redislabs-Solution-Architects/spring-batch-redis'
				}

				licenses {
					license {
						name = 'The Apache License, Version 2.0'
						url = 'http://www.apache.org/licenses/LICENSE-2.0.txt'
					}
				}

				developers {
					developer {
						id = 'jruaux'
						name = 'Julien Ruaux'
					}
				}
			}
		}
	}
}

githubRelease {
    token = project.hasProperty('githubToken') ? project.property('githubToken') : ''
    owner "Redislabs-Solution-Architects"
    repo "spring-batch-redis"
    body changelog()
}

afterReleaseBuild.dependsOn ":githubRelease"
afterReleaseBuild.dependsOn bintrayUpload